; YipOS Installer Script
; For NSIS (Nullsoft Scriptable Install System)

!include "MUI2.nsh"
!include "LogicLib.nsh"
!include "FileFunc.nsh"
!include "WinVer.nsh"

; Define installer name and output file
; Variants (pass on makensis command line):
;   (default)        — CPU-only CT2, no CUDA libs        ~80 MB
;   /DCUDA_LITE      — CUDA CT2, user supplies cuBLAS    ~80 MB
;   /DCUDA_FULL      — CUDA CT2, cuBLAS bundled          ~700 MB
Name "YipOS"
!ifdef CUDA_FULL
    OutFile "..\YipOS v1.1.3 Setup (CUDA).exe"
!else ifdef CUDA_LITE
    OutFile "..\YipOS v1.1.3 Setup (CUDA Lite).exe"
!else
    OutFile "..\YipOS v1.1.3 Setup.exe"
!endif

; Default installation directory
InstallDir "$PROGRAMFILES\YipOS"

; Registry key for uninstaller
!define UNINSTKEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\YipOS"

; Visual C++ Redistributable URL and registry detection
!define VCREDIST_URL "https://aka.ms/vs/17/release/vc_redist.x64.exe"
!define VCREDIST_KEY "SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64"

; Request application privileges
RequestExecutionLevel admin

; Interface settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\assets\yip_os_logo.ico"
!define MUI_UNICON "..\assets\yip_os_logo.ico"

; Variables
Var Dialog
Var VCRedistInstalled
Var InstallVCRedist

; Pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "LICENSE.txt"
!insertmacro MUI_PAGE_DIRECTORY
Page custom VCRedistPage VCRedistPageLeave
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Languages
!insertmacro MUI_LANGUAGE "English"


; Function to check if VC++ Redistributable is installed
Function CheckVCRedist
    StrCpy $VCRedistInstalled "0"
    ReadRegDWORD $0 HKLM "${VCREDIST_KEY}" "Installed"
    ${If} $0 == "1"
        StrCpy $VCRedistInstalled "1"
    ${EndIf}
FunctionEnd

; Visual C++ Redistributable Page
Function VCRedistPage
    Call CheckVCRedist
    ${If} $VCRedistInstalled == "1"
        Abort
    ${EndIf}

    StrCpy $InstallVCRedist "1"

    !insertmacro MUI_HEADER_TEXT "Visual C++ Redistributable" "Install Microsoft Visual C++ Redistributable (recommended)"

    nsDialogs::Create 1018
    Pop $Dialog

    ${If} $Dialog == error
        Abort
    ${EndIf}

    ${NSD_CreateLabel} 0 0 100% 40u "YipOS requires the Microsoft Visual C++ Redistributable to run properly. This component is not installed on your system.$\r$\n$\r$\nIt's recommended to install this component."
    Pop $0

    ${NSD_CreateCheckbox} 0 50u 100% 10u "Install Visual C++ Redistributable"
    Pop $InstallVCRedist
    ${NSD_Check} $InstallVCRedist

    nsDialogs::Show
FunctionEnd

Function VCRedistPageLeave
    ${NSD_GetState} $InstallVCRedist $InstallVCRedist
FunctionEnd

; Installation section
Section "Install"
    SetOutPath "$INSTDIR"

    ; Install Visual C++ Redistributable if needed
    Call CheckVCRedist
    ${If} $VCRedistInstalled == "0"
        ${If} $InstallVCRedist == "1"
            DetailPrint "Downloading Visual C++ Redistributable..."
            NSISdl::download "${VCREDIST_URL}" "$TEMP\vc_redist.x64.exe"
            Pop $0
            ${If} $0 == "success"
                DetailPrint "Installing Visual C++ Redistributable..."
                ExecWait '"$TEMP\vc_redist.x64.exe" /quiet /norestart' $0
                ${If} $0 != "0"
                    DetailPrint "Visual C++ Redistributable installation failed with code $0"
                    MessageBox MB_ICONEXCLAMATION|MB_OK "The Visual C++ Redistributable installation failed. YipOS may not work correctly without it.$\n$\nYou can download and install it manually from: ${VCREDIST_URL}"
                ${Else}
                    DetailPrint "Visual C++ Redistributable installed successfully"
                ${EndIf}
            ${Else}
                DetailPrint "Failed to download Visual C++ Redistributable: $0"
                MessageBox MB_ICONEXCLAMATION|MB_OK "Failed to download the Visual C++ Redistributable. YipOS may not work correctly without it.$\n$\nYou can download and install it manually from: ${VCREDIST_URL}"
            ${EndIf}
            Delete "$TEMP\vc_redist.x64.exe"
        ${EndIf}
    ${EndIf}

    ; Install application files
    SetOutPath "$INSTDIR"
    File "..\build_win\yip_os.exe"

    ; Translation DLLs (optional — present when built with CTranslate2)
    File /nonfatal "..\build_win\ctranslate2.dll"
    File /nonfatal "..\build_win\sentencepiece.dll"
    File /nonfatal "..\build_win\openblas.dll"

    ; CUDA runtime DLLs (only in CUDA Full variant — adds ~600 MB)
    !ifdef CUDA_FULL
        File /nonfatal "..\build_win\cublas64_12.dll"
        File /nonfatal "..\build_win\cublasLt64_12.dll"
        File /nonfatal "..\build_win\cudart64_12.dll"
    !endif

    ; MeCab DLL + config (optional — for Japanese kanji→hiragana)
    File /nonfatal "..\build_win\libmecab.dll"
    File /nonfatal "..\build_win\mecabrc"

    ; MeCab ipadic dictionary (~12 MB — required for Japanese kanji→hiragana)
    SetOutPath "$INSTDIR\mecab-dic\ipadic"
    File /nonfatal "..\build_win\mecab-dic\ipadic\*.*"
    SetOutPath "$INSTDIR"

    ; Assets
    SetOutPath "$INSTDIR\assets"
    File /nonfatal "..\assets\vq_codebook.npy"
    File /nonfatal "..\assets\yip_os_logo.png"

    ; Default images for IMG screen
    SetOutPath "$INSTDIR\assets\images"
    File /nonfatal "..\assets\images\*.*"

    ; Notification sounds
    SetOutPath "$INSTDIR\assets\sounds"
    File /nonfatal "..\assets\sounds\*.*"

    ; Create AppData directories for models, logs, etc.
    CreateDirectory "$APPDATA\yip_os"
    CreateDirectory "$APPDATA\yip_os\models"
    CreateDirectory "$APPDATA\yip_os\models\nllb"

    ; Create shortcuts
    CreateDirectory "$SMPROGRAMS\YipOS"
    CreateShortcut "$SMPROGRAMS\YipOS\YipOS.lnk" "$INSTDIR\yip_os.exe"
    CreateShortcut "$SMPROGRAMS\YipOS\Uninstall.lnk" "$INSTDIR\uninstall.exe"
    CreateShortcut "$DESKTOP\YipOS.lnk" "$INSTDIR\yip_os.exe"

    ; Write uninstaller
    WriteUninstaller "$INSTDIR\uninstall.exe"

    ; Write uninstall information to registry
    WriteRegStr HKLM "${UNINSTKEY}" "DisplayName" "YipOS"
    WriteRegStr HKLM "${UNINSTKEY}" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
    WriteRegStr HKLM "${UNINSTKEY}" "InstallLocation" "$INSTDIR"
    WriteRegStr HKLM "${UNINSTKEY}" "DisplayIcon" "$INSTDIR\yip_os.exe,0"
    WriteRegStr HKLM "${UNINSTKEY}" "Publisher" "Foxipso"
    WriteRegStr HKLM "${UNINSTKEY}" "DisplayVersion" "1.1.3"
    WriteRegStr HKLM "${UNINSTKEY}" "URLInfoAbout" "https://foxipso.com"

    ; Get size of installation directory
    ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
    IntFmt $0 "0x%08X" $0
    WriteRegDWORD HKLM "${UNINSTKEY}" "EstimatedSize" "$0"
SectionEnd

; Uninstallation section
Section "Uninstall"
    ; Remove application files
    Delete "$INSTDIR\yip_os.exe"
    Delete "$INSTDIR\uninstall.exe"
    Delete "$INSTDIR\ctranslate2.dll"
    Delete "$INSTDIR\sentencepiece.dll"
    Delete "$INSTDIR\openblas.dll"
    Delete "$INSTDIR\cublas64_12.dll"
    Delete "$INSTDIR\cublasLt64_12.dll"
    Delete "$INSTDIR\cudart64_12.dll"
    Delete "$INSTDIR\libmecab.dll"
    Delete "$INSTDIR\mecabrc"
    RMDir /r "$INSTDIR\mecab-dic"
    Delete "$INSTDIR\assets\vq_codebook.npy"
    Delete "$INSTDIR\assets\yip_os_logo.png"
    RMDir /r "$INSTDIR\assets\images"
    RMDir /r "$INSTDIR\assets\sounds"
    RMDir "$INSTDIR\assets"
    RMDir "$INSTDIR"

    ; Remove shortcuts
    Delete "$SMPROGRAMS\YipOS\YipOS.lnk"
    Delete "$SMPROGRAMS\YipOS\Uninstall.lnk"
    RMDir "$SMPROGRAMS\YipOS"
    Delete "$DESKTOP\YipOS.lnk"

    ; Ask user if they want to remove settings
    MessageBox MB_YESNO "Do you want to remove your YipOS settings as well?$\n$\nThis will delete your config, logs, and whisper models from AppData." IDNO KeepSettings
    RMDir /r "$APPDATA\yip_os"

    KeepSettings:
    ; Remove registry entries
    DeleteRegKey HKLM "${UNINSTKEY}"
SectionEnd
